SET SERVEROUTPUT ON SIZE 100000

CLEAR SCREEN

DECLARE

   i_cuantos_find    INTEGER;
   i_contador        INTEGER;
   i_cont_err        INTEGER DEFAULT 0;
   i_tot_vue         INTEGER DEFAULT 0;

   b_show_columnas   BOOLEAN DEFAULT true;--Bandera para mostrar o no las columnas

   v_propietario     VARCHAR2 (255) := 'OWNER';
   v_busqueda        VARCHAR2 (4000) := 'PALABRA A BUSCAR';
   v_tipo_dato       VARCHAR2 (255) := 'TIPO DE DATO';
   v_registro        VARCHAR (255);
   v_tip             VARCHAR (255) := ' de la busqueda '''|| v_busqueda ||''', ejecute el query anterior para ver resultados';

   c_columnas        CLOB := '';
   c_query_sql       CLOB := '';
   c_exec_sql        CLOB := '';

BEGIN
   FOR v_todas_tablas
      IN (  SELECT OWNER, TABLE_NAME
              FROM all_tables
             WHERE OWNER = v_propietario
               AND TABLE_NAME IN (SELECT TABLE_NAME
                                    FROM all_tab_columns
                                   WHERE OWNER = all_tables.OWNER
                                     AND DATA_TYPE LIKE '%'|| UPPER (v_tipo_dato) ||'%')
          ORDER BY TABLE_NAME)
   LOOP

      i_contador := 0;
      c_query_sql := '';
      c_exec_sql := '';
      c_columnas := '';

      FOR v_todas_columnas
         IN (SELECT COLUMN_NAME, TABLE_NAME
               FROM all_tab_columns
              WHERE OWNER = v_propietario
                AND TABLE_NAME = v_todas_tablas.TABLE_NAME
                AND DATA_TYPE LIKE '%' || UPPER (v_tipo_dato) || '%')
      LOOP
      
         IF i_contador > 0
         THEN
            c_query_sql := c_query_sql || ' OR ';
            c_columnas := c_columnas || ', ';
         END IF;

         IF v_todas_columnas.COLUMN_NAME IS NOT NULL
         THEN
            c_columnas := c_columnas || v_todas_columnas.COLUMN_NAME;
            --Obtener el query para ejecutar dentro de la consulta
            c_query_sql := c_query_sql || 'UPPER(' || v_todas_columnas.COLUMN_NAME || ') LIKE ''%'|| UPPER (v_busqueda)||'%''';
            i_contador := i_contador + 1;            
         END IF;

      END LOOP;

      IF c_query_sql IS NOT NULL
      THEN
         --Estructurar Querys para mostrar
         c_exec_sql := 'SELECT * FROM ' || v_propietario || '.' || v_todas_tablas.TABLE_NAME || ' WHERE ' || c_query_sql || ';';
         c_query_sql := 'SELECT COUNT(*) FROM ' || v_propietario || '.' || v_todas_tablas.TABLE_NAME || ' WHERE ' || c_query_sql;
         i_tot_vue := i_tot_vue +1;
      END IF;

      EXECUTE IMMEDIATE c_query_sql 
      INTO i_cuantos_find;

      IF i_cuantos_find > 0
      THEN
        IF i_cuantos_find < 2 
          THEN v_registro := ' se encontrÃ³ ' || i_cuantos_find || ' registro' || v_tip; 
          ELSE v_registro := ' se encontraron ' || i_cuantos_find || ' registros' || v_tip; 
        END IF;

        DBMS_OUTPUT.put_line('La Consulta es: '|| c_exec_sql);
        DBMS_OUTPUT.put_line('El Propietario ' || v_todas_tablas.OWNER || ', en la tabla ' || v_todas_tablas.TABLE_NAME || v_registro);
        IF b_show_columnas = TRUE
          THEN
            DBMS_OUTPUT.put_line('Las columnas de la tabla donde se busco son: ' || c_columnas);
        END IF;            
        DBMS_OUTPUT.put_line('----------------------------------------------------------------');
      ELSE
         i_cont_err := i_cont_err+1;
      END IF;

   END LOOP;

   IF i_cont_err = i_tot_vue THEN
        DBMS_OUTPUT.put_line ('No se encontraron registros, por favor revisa la palabra o frase a buscar');            
   END IF;

EXCEPTION
   WHEN NO_DATA_FOUND
   THEN
      DBMS_OUTPUT.put_line ('No se encontraron registros, por favor revisa la palabra o frase a buscar');
   WHEN OTHERS
   THEN
      DBMS_OUTPUT.put_line ('Error al ejecutar la sentencia WHERE: '|| DBMS_LOB.SUBSTR (c_query_sql, 255));
END;
/
